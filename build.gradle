plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
	id "com.modrinth.minotaur" version "2.+"
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.archives_base_name
}

repositories {
	maven {
		url 'https://api.modrinth.com/maven'
		content {
			includeGroup "maven.modrinth"
		}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/expanded_weaponry.accesswidener")
}

fabricApi {
	configureDataGeneration {
		client = true
	}
}
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	mappings loom.officialMojangMappings()

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
	include(modApi("maven.modrinth:kielsons-api:${project.kielsonsapi_version}"))
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	repositories {

	}
}

modrinth {
	token = project.modrinth_token
	projectId = "q8rZUpjS"
	versionNumber = String.valueOf(project.mod_version)
	versionType = project.version_type
	uploadFile = remapJar
	gameVersions = [String.valueOf(project.minecraft_version)]
	loaders = ["fabric"]
	dependencies {
		required.project "fabric-api"
		embedded.project "kielsons-api"
	}
}